buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.5.2"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1"
  }
}

apply plugin: "com.palantir.git-version"
def getVersionWithoutDirtySuffix = { ->
  def lastTag = versionDetails().lastTag
  if (lastTag.endsWith(".dirty")) {
    return lastTag.substring(0, lastTag.indexOf(".dirty"));
  }
  return lastTag;
}

group = 'com.teslagov'
version = getVersionWithoutDirtySuffix()
description = """GDELT Java SDK"""

subprojects {
  apply plugin: 'idea'
  apply plugin: 'java'
  sourceCompatibility = 1.8
}

ext {
  githubOrgName = 'TeslaGov'
  githubRepoName = 'gdelt'
  inceptionYear = 2016

  bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  bintrayOrgName = 'teslagov'

  ossrhUser = project.property('nexusUsername')
  ossrhPass = project.property('nexusPassword')
  passPhrase = project.property('signing.password')
  labels = ['gdelt']

  slf4jVersion = "1.7.21"
  junitVersion = '4.12'
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.0'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}