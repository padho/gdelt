apply plugin: 'maven-publish'

// TODO the new maven-publish plugin has no support for signing artifacts...
// TODO checkout http://mike-neck.github.io/blog/2013/06/21/how-to-publish-artifacts-with-gradle-maven-publish-plugin-version-1-dot-6/
// https://docs.gradle.org/current/userguide/publishing_maven.html
task sourceJar(type: Jar) {
    classifier 'sources'
    baseName = rootProject.name + "-" + project.name
    version = rootProject.bintrayVersion
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    baseName = rootProject.name + "-" + project.name
    version = rootProject.bintrayVersion
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            // telling gradle to publish project's jar archive
            from components.java
            groupId rootProject.groupId
            artifactId project.artifactId
            version project.version

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', "https://github.com/${githubOrgName}/${githubRepoName}")
                root.appendNode('inceptionYear', inceptionYear.toString())

                def scm = root.appendNode('scm')
                scm.appendNode('url', "https://github.com/${githubOrgName}/${githubRepoName}")
                scm.appendNode('connection', "scm:git:git://github.com/${githubOrgName}/${githubRepoName}.git")
                scm.appendNode('developerConnection', "scm:git://github.com/${githubOrgName}/${githubRepoName}.git")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'MIT License')
                license.appendNode('url', 'http://www.opensource.org/licenses/mit-license.php')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')

                def developer1 = developers.appendNode('developer')
                developer1.appendNode('id', 'kevinmichaelchen')
                developer1.appendNode('name', 'Kevin Chen')
                developer1.appendNode('email', 'kevin.chen.bulk@gmail.com')
                developer1.appendNode('url', 'https://github.com/kevinmichaelchen')

                def developer2 = developers.appendNode('developer')
                developer2.appendNode('id', 'crain9412')
                developer2.appendNode('name', 'Jon Crain')
                developer2.appendNode('email', 'jon.crain@teslagovernment.com')
                developer2.appendNode('url', 'https://github.com/crain9412')
            }
        }
    }
}

publish.doLast {
    println 'FINISHED PUBLISHING!'
}
